@ Every positive integer can be represented as the sum of three
@ palindromes.
@ Implementation of the proof in the paper by Javier Cilleruelo,
@ Florian Luca and Lewis Baxter
@ number --> palidrome-1 | palindrome-2 | palindrome-3
\<< DUP I\->NL DUP SIZE \-> num d n
    \<< IF n 7 < THEN
            num SMALL
        ELSE
            d NTYPE \-> type special d p1 p2 p3
            \<< IF { "A1" "A2" "A3" "A4" } type POS
                   n 2 MOD AND
                   { "A5" "A6" } type POS
                   n 2 MOD NOT AND OR
                   special NOT AND
                THEN
                    d p1 p2 p3 ALGO1
                ELSE IF { "A5" "A6" } type POS
                        n 2 MOD AND
                        { "A1" "A2" "A3" "A4" } type POS
                        n 2 MOD NOT AND OR
                        special NOT AND
                THEN
                    d p1 p2 p3 ALGO2
                ELSE IF { "B1" "B2" "B3" "B4" "B5" "B6" "B7" } type POS
                        n 2 MOD AND
                        special NOT AND
                THEN
                    d p1 p2 p3 ALGO3
                ELSE IF special NOT THEN
                    @ Type B - normal number with odd number of digits
                    d p1 p2 p3 ALGO4
                ELSE
                    @ Special numbers
                    num p1 ALGO5
                END END END END
            \>>
        END
    \>>
\>>
'PALIN' STO
