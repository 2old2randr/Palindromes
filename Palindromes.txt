@ Every positive integer can be represented as the sum of three
@ palindromes.
@ Implementation of the proof in the paper by Javier Cilleruelo,
@ Florian Luca and Lewis Baxter
@ number --> palidrome-1 | palindrome-2 | palindrome-3
\<< DUP I\->NL DUP SIZE \-> num d n
    \<< IF n 7 < THEN
            num SMALL
            IF DUP TYPE 5 == THEN
                LIST\-> DROP @ make output consistently 3 nums on stack
            END
        ELSE
            d NTYPE \-> type special d p1 p2 p3
            \<< IF special THEN
                    num p1 ALGO5
                ELSE IF { "A1" "A2" "A3" "A4" } type POS
                   n 2 MOD AND
                   { "A5" "A6" } type POS
                   n 2 MOD NOT AND OR
                THEN
                    d p1 p2 p3 ALGO1
                ELSE IF type 1 1 SUB "A" == THEN
                    @ type A1-A4 and even digits or A5-A6 with odd
                    d p1 p2 p3 ALGO2
                ELSE IF n 2 MOD THEN
                    @ type B with odd number of digits
                    d p1 p2 p3 ALGO3
                ELSE
                    @ Type B with even number of digits
                    d p1 p2 p3 ALGO4
                END END END END
            \>>
        END
    \>>
\>>
'PALIN' STO
